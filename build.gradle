plugins {
	id 'net.minecrell.licenser' version '0.3'
	id "us.kirchmeier.capsule" version "1.0.2"
}

apply plugin: 'java'

apply plugin: 'eclipse'
apply plugin: 'idea'

group = 'com.surgeplay'
def commits = 'git rev-list --count HEAD'.execute().in.text.trim();
def dirty = !'git diff-index HEAD'.execute().in.text.trim().isEmpty();
version = '2.0.'+commits+(dirty ? '-dirty' : '');
archivesBaseName = 'Visage'

def commitHash = 'git rev-parse HEAD'.execute().in.text.trim();
if (commitHash.length() != 40) {
	commitHash = 'master';
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

import org.gradle.internal.os.OperatingSystem

switch ( OperatingSystem.current() ) {
	case OperatingSystem.WINDOWS:
		project.ext.lwjglNatives = "natives-windows"
		break
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
	break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = "natives-macos"
		break
}

repositories {
	mavenCentral()
	mavenLocal()
}

project.ext.lwjglVersion = "3.1.2"

dependencies {
	compile 'com.google.guava:guava:22.0'
	compile 'net.sf.jopt-simple:jopt-simple:5.0.3'
	compile 'com.typesafe:config:1.3.1'
	compile 'com.rabbitmq:amqp-client:4.1.1'
	compile 'com.google.code.gson:gson:2.8.1'
	compile 'org.fusesource.jansi:jansi:1.16'
	compile 'com.github.kerner1000:javapng:2.1.2'
	compile 'org.slf4j:slf4j-jdk14:1.7.25'
	
	compile "org.lwjgl:lwjgl:${lwjglVersion}"
	compile "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
	compile "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}"
	compile "org.lwjgl:lwjgl-opengl:${lwjglVersion}"
	compile "org.lwjgl:lwjgl-stb:${lwjglVersion}"
	
	runtime "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
	runtime "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${lwjglNatives}"
	runtime "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}:${lwjglNatives}"
	runtime "org.lwjgl:lwjgl-opengl:${lwjglVersion}:${lwjglNatives}"
	runtime "org.lwjgl:lwjgl-stb:${lwjglVersion}:${lwjglNatives}"
	
	compile 'org.eclipse.jetty:jetty-server:9.4.6.v20170531'
	compile 'redis.clients:jedis:2.9.0'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	options.deprecation = true
}

task capsule(type: MavenCapsule) {
	applicationClass 'com.surgeplay.visage.Visage'
	capsuleManifest {
		systemProperties['com.surgeplay.visage.internal.builtFrom'] = commitHash;
		systemProperties['com.surgeplay.visage.internal.version'] = project.version;
	}
}

license {
	header = project.file('LICENSE')
	
	matching('**/com/github/steveice10/mc/auth/**') {
		header = project.file('mcauthlib-LICENSE')
	}
}